buildscript {
    repositories {
        // Maven central JARs now have Java 9 classes, and Forge can't consume that unfortunately...
        maven { url = 'https://maven.minecraftforge.net/' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'java'

repositories {
    maven { url = 'https://maven.minecraftforge.net/' }
}

version = '4.8.2-alpha'
group = 'com.lion328.thaifixes'
archivesBaseName = 'thaifixes'

def coremodClass = 'com.lion328.thaifixes.coremod.CoremodLoader'

compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

minecraft {
    version = '1.12.2-14.23.5.2847'
    runDir = 'run'

    mappings = "snapshot_20171003"
    clientJvmArgs = ["-Dfml.coreMods.load=${coremodClass}"]
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes([
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLCorePlugin': coremodClass,
        ])
    }
}
