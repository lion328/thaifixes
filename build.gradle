buildscript {
    repositories {
        // Maven central JARs now have Java 9 classes, and Forge can't consume that unfortunately...
        maven { url = 'https://maven.minecraftforge.net/' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'java'

repositories {
    maven { url = 'https://maven.minecraftforge.net/' }
}

version = '4.8.2-alpha'
group = 'com.lion328.thaifixes'
archivesBaseName = 'thaifixes'

def coremodClass = 'com.lion328.thaifixes.coremod.CoremodLoader'

minecraft {
    version = '1.12.2-14.23.5.2847'
    runDir = 'run'

    mappings = 'snapshot_20171003'
    clientJvmArgs = ["-Dfml.coreMods.load=${coremodClass}"]
}

def replaceVars = [
        'name': 'ThaiFixes',
        'version': project.version,
        'modid': project.archivesBaseName,
        'mcversion': project.minecraft.version,
]

sourceSets {
    main {
        java.srcDir "$buildDir/generated/java"
        // Make Intellij see assets
        output.resourcesDir = output.classesDir
    }
    template.java.srcDir 'src/template/java'
}

task generateSources(type: Copy) {
    inputs.properties replaceVars

    from(sourceSets.template.java.srcDirs) {
        expand replaceVars
    }

    into "$buildDir/generated/java"
}

sourceCompatibility = targetCompatibility = 1.8
compileJava {
    dependsOn generateSources
    sourceCompatibility = targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.properties replaceVars

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand replaceVars
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes([
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLCorePlugin': coremodClass,
        ])
    }
}
